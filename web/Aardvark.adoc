= Aardvark: The Anatomy of a Large-Scale Social Search Engine
:neo4j-version: 2.3.0
:author: Cristina Escalante
:twitter:
:tags: social

:toc:

== Introduction to Aardvark, the Defunct Social Search Engine

=== The Gist of It...

Aardvark (2008 - 2011) was a social search service that connected users live with friends or friends-of-friends who were able to answer their questions.
Users submitted questions via the Aardvark website, email or instant messenger.
Aardvark then connected the user the person in the user’s social network most likely to be able to answer that question.

In contrast to ‘conventional’ search engines, which find the best _document_ to respond to the query, Aardvark determined the best _person_ to satisfy a user’s information needs.

=== Posing a Query

User Sam joins Aardvark, and connects to some buddies from his social network.

Sam wants to know more about the topic of elephants, and poses the question:

----
"What do elephants eat during the wintertime?"
----

In reality, the question would be more precise (like below) and less findable on Wikipedia, but the premise is the same.
----
“Do you have any good babysitter recommendations in Palo Alto for my 6-year-old twins?I’m looking for somebody that won’t let them watch TV."
----

=== Responding to a Query

After receiving the query, the Conversation Manager (in this example, me, in reality, a computer) determines that it is a valid query and what topic it belongs to.

In this example, the topic is "elephant".

The Community Manager then informs the user of the topic of the query and asks potential respondents from Sam's social network if they'd like to respond to the query.
Once a respondent is found, the response to the query sent to Sam, who then has the opportunity to ask followup questions.

Aardvark periodically updates its user ranking algorithm (to be defined below) so users expert in topics that have buddies also expert in those topics have higher ranking than isolated experts.


== Aardvark: Main Components

- *Crawler and Indexer*
-- Finds and labels resources (in this case, users) that contain information
- *Query Analyzer*
-- "Understands" the information need of a user
- *Ranking Function*
-- Selects the best resource to answer a question
- *UI*
-- Presents information to the user in an easy-to-process form

== The Model

The core of Aardvark is a statistical model, described in two parts.

=== Relevance Score

The probability that user _i_ will successfully answer question _q_, given that question _q_ is about topics _t_ that user _i_ is knowledgable about.

++++
\[p(u_i \mid q) = \sum p(u_i \mid t)p(t \mid q) \]
++++

=== Quality Score

A query-indpendent probability of success for a pair of users _i_ and _j_, denoted

++++
\[p(u_i \mid u_j)\]
++++

In other words, the probability that user _i_ will deliver a satisfactory answer to _j_ no matter the question.

Aardvark used a variety of methods to calcuate this score, including profile similarity from data supplied by user profiles, social connectedness, and other shared characteristics.

=== Scoring

The scoring function, with parameters user _i_, user _j_, and question _q_ is the composition of the two above probabilities.
The goal is to combine the query-dependent relevance score and the query-independent quality score.

++++
\[s(u_i, u_j, q) = p(u_i \mid u_j) \cdot p(u_i \mid q)\]
++++

=== Connections

Aardvark took into account the following when analyzing the connection between users _i_ and _j_.

- Social connection
- Shared demographic features
- Shared profile features
- Shared vocabulary (from the queries?)
- Match of politeness (example, ending messages with "Thanks")
- Speed of responsiveness to other users

Connection strengths between users are computed with a weighted cosine similarity over the feature set described above, normalized so that:

++++
\[\sum p(u_i \mid u_j) = 1\]
++++

=== Analyzing Questions

Step 1: Basic Filering

- _NonQuestionClassifier_ determines whether input is a question
- _InappropriateQuestionClassifier_ determines if input is obscene or spam
- _TrivialQuestionClassifier_ determines if the question is easily answered by an existing service (example, "What is the capital of Alaska?")
- _LocationSensitiveClassifier_ determines if the question is location-based

Step 2: Generating Topics

- _KeywordMatchTopicMapper_ does a string match between the topics in the query and topics in user profiles
- _TaxonomyTopicMapper_ classifies the query type into a taxonomy of over 3000 popular question topics
- _SalientTermTopicMapper_ extracts salient phrases and finds semantically similar topics
- _UserTagTopicMapper_ uses user-supplied 'tags' and maps these to semantically similar user topics

Outputs from these classifiers were combined using weighted linear combinations, and were continuously evaluated by human judges.


== Analysis

=== Usage and Performance

- Opened for beta release in March 2009, Aardvark had 90,361 active users, 225,047 questions and 386,702 answers
- The majority of users were mobile users
- Questions were highly contextualized, with an average query length of 18.6 words, in contrast to query lengths in regular search engines, which are usually between two and three words.
- Lots of subjective ("what do you think...")  questions
- Questions were answered quickly: 87% of questions submitted received at least 1 answer, and 57% received their first answer in less than 10 minutes
- Social proximity matters: answers from within the asker's network (most frequently a second degree connection) were rated as _good_ (the highest ranking) about 76% of the time, whereas those outside the asker's network were rated as _good_ about 68% of the time

== Creating a Social Search Engine: A Prototype

//hide
//setup
[source,cypher]
----
//create the user nodes
CREATE (user_1:User {id:1, name:'Jacob'} ), (user_2:User {id:2, name:'David'} ), (user_3:User {id:3, name:'Sam'} ), (user_4:User {id:4, name:'Mohammed'} ), (user_5:User {id:5, name:'Cristina'} ), (user_6:User {id:6, name:'Josh'} ), (user_7:User {id:7, name:'Andrea'} ), (user_8:User {id:8, name:'Hill'} ), (user_9:User {id:9, name:'Bart'} ), (user_10:User {id:10, name:'Greg'} )

//create the topic nodes
CREATE (topic_1:Topic {id:1, name:'aardvark'} ), (topic_2:Topic {id:2, name:'dugong'} ), (topic_3:Topic {id:3, name:'hyrax'} ), (topic_4:Topic {id:4, name:'sengis'} ), (topic_5:Topic {id:5, name:'golden-mole'} ), (topic_6:Topic {id:6, name:'tenrec'} ), (topic_7:Topic {id:7, name:'elephant'} ), (topic_8:Topic {id:8, name:'stegedont'} ), (topic_9:Topic {id:9, name:'manatee'} ), (topic_10:Topic {id:10, name:'mammoth'} )

//create the user-user relationships
CREATE (user_1)-[:IS_BUDDY]->(user_5), (user_1)-[:IS_BUDDY]->(user_2), (user_1)-[:IS_BUDDY]->(user_8), (user_2)-[:IS_BUDDY]->(user_4), (user_2)-[:IS_BUDDY]->(user_6), (user_2)-[:IS_BUDDY]->(user_8), (user_3)-[:IS_BUDDY]->(user_9), (user_3)-[:IS_BUDDY]->(user_7), (user_3)-[:IS_BUDDY]->(user_3), (user_4)-[:IS_BUDDY]->(user_5), (user_4)-[:IS_BUDDY]->(user_9), (user_4)-[:IS_BUDDY]->(user_2), (user_5)-[:IS_BUDDY]->(user_10), (user_5)-[:IS_BUDDY]->(user_1), (user_5)-[:IS_BUDDY]->(user_4), (user_7)-[:IS_BUDDY]->(user_3), (user_7)-[:IS_BUDDY]->(user_2), (user_7)-[:IS_BUDDY]->(user_5), (user_8)-[:IS_BUDDY]->(user_3), (user_8)-[:IS_BUDDY]->(user_3), (user_8)-[:IS_BUDDY]->(user_1), (user_9)-[:IS_BUDDY]->(user_2), (user_9)-[:IS_BUDDY]->(user_3), (user_9)-[:IS_BUDDY]->(user_5), (user_10)-[:IS_BUDDY]->(user_3), (user_10)-[:IS_BUDDY]->(user_4), (user_10)-[:IS_BUDDY]->(user_8)

//create the user-topic relationships
CREATE (user_1)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_10), (user_1)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_5), (user_1)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_2), (user_2)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_5), (user_2)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_8), (user_2)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_1), (user_2)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_3), (user_3)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_10), (user_3)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_7), (user_3)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_8), (user_3)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_3), (user_4)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_6), (user_4)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_3), (user_4)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_6), (user_5)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_10), (user_5)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_5), (user_5)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_2), (user_5)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_3), (user_7)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_4), (user_7)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_1), (user_7)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_6), (user_7)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_3), (user_8)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_4), (user_8)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_7), (user_8)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_6), (user_9)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_7), (user_9)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_6), (user_9)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_3), (user_10)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_8), (user_10)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_7), (user_10)-[:KNOWS_ABOUT {right:1, total:2 }]->(topic_9)

RETURN *
----
// graph_result

== Making a Query

=== Relevance Score

Let's say user Josh wants to search his social network for someone who knows about hyraxes.
Since this is the first time the network has been used, and this is a simple example, everyone will start out with the same level knowledge of their assigned topics.

[source,cypher]
----
MATCH (a:User {name:'Josh'})-[:IS_BUDDY*1..2]-(b:User)-[r:KNOWS_ABOUT]-(:Topic {name:'hyrax'})
RETURN DISTINCT b.name AS Name, (r.right)/toFloat(r.total) AS Score
ORDER BY Score DESC
----
//table

Since all options are the same, let's pose David a question about hyraxes that he will respond to correctly.

[source,cypher]
----
MATCH (a:User {name:'Josh'}),(b:User {name:'David'})-[r:KNOWS_ABOUT]-(t:Topic {name:'hyrax'})
MERGE (a)-[:ASKS]->(q1:query {id:1, text:"What is your favourite type of hyrax?"})
MERGE (q1)-[:RESPONDENT {correct:1}]->(b)
MERGE (q1)-[:ABOUT]->(t)
SET r.right = r.right +1
SET r.total = r.total +1
RETURN a,q1,b,t
----

Let's see what happens when another user, let's say, Jacob, looks for a good candidate for responding to questions about hyraxes now:

[source,cypher]
----
MATCH (a:User {name:'Jacob'})-[:IS_BUDDY*1..2]-(buddy:User)-[r:KNOWS_ABOUT]->(:Topic {name:'hyrax'})
RETURN DISTINCT buddy.name AS Name, (r.right)/toFloat(r.total) AS Score
ORDER BY Score DESC
----
//table

Since David has responded to a question about hyraxes correctly, his score on the topic of hyraxes is now higher than the default .5.
Let's put more questions in the system.
Now Mohammed has asked David two questions, one he responds to correctly, and one he responds to incorrectly.

//hide
[source,cypher]
----
MATCH (a:User {name:'Mohammed'}),(b:User {name:'David'})-[r1:KNOWS_ABOUT]->(t1:Topic {name:'aardvark'}),
(b)-[r2:KNOWS_ABOUT]-(t2:Topic {name:'stegedont'})
MERGE (a)-[:ASKS]->(q3:query {id:3, text:"What is your favourite type of aardvark?"})
MERGE (q3)-[:RESPONDENT {correct:1}]->(b)
MERGE (q3)-[:ABOUT]->(t1)
SET r1.right = r1.right + 1
SET r1.total = r1.total +1

MERGE (a)-[:ASKS]->(q4:query {id:4, text:"What is your favourite type of stegedont?"})
MERGE (q4)-[:RESPONDENT {correct:0}]->(b)
MERGE (q4)-[:ABOUT]->(t2)
SET r2.right = r2.right
SET r2.total = r2.total +1
----

For comparison, Jacob has also asked David two more questions, both of which he answers correctly.

//hide
[source,cypher]
----
MATCH (a:User {name:'Jacob'}),(b:User {name:'David'})-[r1:KNOWS_ABOUT]-(t1:Topic {name:'hyrax'}),
(b)-[r2:KNOWS_ABOUT]-(t2:Topic {name:'golden-mole'})
MERGE (a)-[:ASKS]->(q3:query {id:3, text:"What is your favourite type of hyrax?"})
MERGE (q3)-[:RESPONDENT {correct:1}]->(b)
MERGE (q3)-[:ABOUT]->(t1)
SET r1.right = r1.right + 1
SET r1.total = r1.total +1

MERGE (a)-[:ASKS]->(q4:query {id:4, text:"What is your favourite type of golden-mole?"})
MERGE (q4)-[:RESPONDENT {correct:1}]->(b)
MERGE (q4)-[:ABOUT]->(t2)
SET r2.right = r2.right + 1
SET r2.total = r2.total +1
----

Let's take a look at the changes:

//hide
[source,cypher]
----
MATCH (a:User)-[:IS_BUDDY]-(b:User {name: 'David'}), (b)-[r:KNOWS_ABOUT]-(t:Topic)
WHERE a.name = 'Mohammed'OR a.name =  'Jacob'
RETURN DISTINCT  sum(DISTINCT r.right) AS `Correct Responses to Topic`,sum(DISTINCT r.total) AS `Total Queries on Topic`,t.name AS `Topic`
----
//table

=== Quality Score

Since this a small model made with a random number generator (semi-important detail as the users aren't clustering like an actual social group), the users don't have that many shared characteristics.
However, we can still take a look at the graph.
Let's see who might be the best responder to any question a particular user might have.

//hide
[source,cypher]
----
MATCH (a:User)-[:IS_BUDDY]->(b:User), (c:User)-[:IS_BUDDY]-(b), (a)-[r:IS_BUDDY]-(c)
WHERE NOT a=c AND NOT a=b
RETURN  a.name AS `User A`, b.name AS `User B`,count(DISTINCT c) AS `Shared Buddies`
ORDER BY `Shared Buddies` DESC, `User A` ASC
----
//table

== Conclusion

Although defunct, Aardvark did a decent job at finding to responses to subjective questions.
It was an excellent demonstration of how classification and filtering, in combination with social networking, can be leveraged in future search engines.

== Reference

The paper: http://www.cse.fau.edu/~xqzhu/courses/cap6777/social.search.engine.pdf

//console
